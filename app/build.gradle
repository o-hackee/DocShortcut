import java.text.SimpleDateFormat

plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

ext.prepareVersionCode = {
    def buildNumber = System.getenv("BUILD_NUMBER") as String ?: "1"
    buildNumber = buildNumber as Integer
    buildNumber %= 100 // take the last 2 digits
    def buildString = (buildNumber as String).padLeft(2, '0')

    def date = new SimpleDateFormat("yyyyMMdd").format(new Date())
    def versionCode = date + buildString

    println("versionCode: $versionCode")
    return versionCode as Integer
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release {
            storeFile file(keystoreProperties["storeFile"])
            storePassword keystoreProperties["storePassword"]
            keyAlias keystoreProperties["keyAlias"]
            keyPassword keystoreProperties["keyPassword"]
        }
    }
    compileSdk 31

    defaultConfig {
        applicationId "ol.ko.docshortcut"
        minSdk 23
        targetSdk 31
        versionCode prepareVersionCode()
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'

    implementation 'androidx.datastore:datastore-preferences:1.0.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2-native-mt'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2-native-mt'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.0'
    implementation 'androidx.work:work-runtime-ktx:2.7.1'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test.ext:junit-ktx:1.1.3'
    testImplementation 'androidx.test:core-ktx:1.4.0'
    testImplementation 'org.robolectric:robolectric:4.6.1'
    testImplementation 'androidx.test.espresso:espresso-intents:3.5.0-alpha03' // https://github.com/robolectric/robolectric/issues/6593
    testImplementation 'io.mockk:mockk:1.12.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.work:work-testing:2.7.1'
}